import platform

env = Environment(PIC = '24FJ128GB206', 
                  CC = 'xc16-gcc', 
                  PROGSUFFIX = '.cof', 
                  CFLAGS = '-g -omf=coff -x c -mcpu=$PIC', 
                  LINKFLAGS = '-omf=coff -mcpu=$PIC -Wl,--script="app_p24FJ128GB206.gld"', 
                  CPPPATH = '../lib')

if platform.system() == 'Linux':
    env.PrependENVPath('PATH', '/opt/microchip/xc16/v1.24/bin')
elif platform.system() == 'Darwin':
    env.PrependENVPath('PATH', '/Applications/microchip/xc16/v1.24/bin')

bin2hex = Builder(action = 'xc16-bin2hex $SOURCE -omf=coff',
                  suffix = 'hex', 
                  src_suffix = 'cof')
env.Append(BUILDERS = {'Hex' : bin2hex})
list = Builder(action = 'xc16-objdump -S -D -omf=coff $SOURCE > $TARGET', 
               suffix = 'lst', 
               src_suffix = 'cof')
env.Append(BUILDERS = {'List' : list})

env.Program('analogRead', ['analogRead.c',
                      '../lib/timer.c',
                      '../lib/ui.c',
                      '../lib/pin.c',
                      '../lib/uart.c'])

env.Program('helloMotor', ['helloMotor.c',
                      '../lib/timer.c',
                      '../lib/ui.c',
                      '../lib/pin.c'])

env.Program('hello', ['hello.c',
                      '../lib/uart.c',
                      '../lib/pin.c'])

env.Hex('analogRead')
env.List('analogRead')

env.Hex('helloMotor')
env.List('helloMotor')

env.Hex('hello')
env.List('hello')
